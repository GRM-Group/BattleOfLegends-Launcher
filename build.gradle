apply plugin: 'java'
apply plugin: 'pmd'
apply plugin: 'application'

version = "0.0.0"
group= "pl.grm"
description = 'BOL Game Launcher'
archivesBaseName = "BattleOfLegends-Launcher"
mainClassName = 'pl.grm.boll.Launcher'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile files('libs/ini4j-0.5.2-SNAPSHOT.jar')
}

jar {
    manifest {
        attributes 'Main-Class': 'pl.grm.boll.Launcher'
    }
}

task writePom << {
  pom {
    project {
      inceptionYear '2014'
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }.writeTo("$projectDir/pom.xml")
}
 
pmd {
  ignoreFailures = true
  sourceSets = [sourceSets.main]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSets = [
    "basic",
    "braces"
  ]
}

check << {
  File outDir = new File('build/reports/pmd/')
  outDir.mkdirs()
  ant.taskdef(name: 'cpd', classname: 'net.sourceforge.pmd.cpd.CPDTask',
    classpath: configurations.pmd.asPath)
  ant.cpd(minimumTokenCount: '100', format: 'xml',
    outputFile: new File(outDir , 'cpd.xml')) {
    fileset(dir: "src/main/java") {
      include(name: '**/*.java')
    }
  }
}
